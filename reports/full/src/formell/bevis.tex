\section{Formellt bevis av Toom-Cook}
\label{sec:formellbevis}
Det här avsnittet presenterar det formella beviset för att den implementerade
algoritmen är korrekt. Många av detaljerna i beviset är rent tekniska och
förklaras inte närmare. Jämförelser görs med det informella beviset i avsnitt
\ref{sec:informhuvudbevis}. De viktigaste taktikerna som används förklaras under
bevisets gång. Variablerna \C{p} och \C{q} betecknar genomgående polynom över
ett integritetsområde.

I beviset används, när så är möjligt, redan tidigare bevisade resultat från
\ssr{}s bibliotek. En lång rad lemman om bland annat grundläggande egenskaper
hos polynom, naturliga tal, matriser och summor finns tillgängliga.

Det formella bevisets struktur är modellerad efter det informella bevisets i
avsnitt~\ref{sec:informhuvudbevis}, med ett huvudbevis som bygger på ett
flertal lemman. De formella lemmana är dock många fler än de för det
informella beviset, 15 jämfört med 2. Det beror delvis på att påståenden delats
upp i flera delpåståenden för att underlätta arbetsfördelningen inom gruppen,
men i första hand på att resonemangssteg som i det informella beviset ses som
så triviala att de inte ens nämns också måste visas när beviset formaliseras.
En ytterligare anledning är att det är lättare att göra omskrivningar och
slutledningssteg på mindre delpåståenden separat. Ett 50-tal mer generella
lemman ur \ssr{}-biblioteket och ett par lemman som byggde på grundläggande
egenskaper hos polynomdivision som gick utanför projektets avgränsningar och
som tillhandahållits av projektets handledare, Anders Mörtberg, har använts i
huvudbeviset och i beviset av lemmorna.

Nedan beskrivs huvudbeviset rad för rad, och därefter beskrivs några av de mer
intressanta lemmorna utan bevis.

\subsection{Huvudbeviset}
Detta är den formella motsvarigheten till prop~\ref{prop:1} för den
implementerade algoritmen. Eftersom \C{toom_cook} bara utvärderar
\C{toom_cook_rec} med ett visst argument så beror dess korrekthet helt på
följande sats, som säger att \C{toom_cook_rec} är korrekt:
\begin{lstlisting}
Lemma toom_cook_rec_correct : forall (n : nat) p q,
  unitmx V_I -> toom_cook_rec n p q = p * q.
\end{lstlisting}
När detta lemma är bevisat följer \C{toom_cook_correct} direkt:
\begin{lstlisting}
Lemma toom_cook_correct : forall p q,
  toom_cook p q = p * q.
Proof. move=> p q. by apply: toom_cook_rec_correct. Qed.
\end{lstlisting}
Det formella beviset för att den rekursiva delen av algoritmen är korrekt har
en struktur som liknar beviset av prop~\ref{prop:1}.
% Konstig mening svårt att förstå vad som menas:
Den största skillnaden är att induktionen i det formella fallet görs över antalet
rekursiva anrop av algoritmen istället för att som i beviset av
proposition~\ref{prop:1} göra induktionen över graden av de polynom som multipliceras. Det beror
på att den rekursiva delen av den implementerade algoritmen \C{toom_cook_rec}
är definierad med en oberoende variabel n, så som beskrivits i
\ref{sec:formrec}. I stället för att induktionen som i det informella
beviset bygger på att graden av de polynom som Toom-Cook anropas med minskar
vid rekursiva anrop av algoritmen bygger den på att variabeln n minskas vid
rekursiva anrop. I båda fallen innebär dock induktionsantagandet att vi antar
att algoritmen fungerar korrekt för nästa lägre rekursiva anrop.
%Det nästa lägre anrop, finns det något bättre sätt att säga det här?
Den andra stora skillnaden mellan det informella och det formella beviset är
att många tekniska detaljer, som kan tas för givna i ett informellt bevis måste
visas explicit. Dessutom måste hänsyn tas till hur matriser och polynom är
representerade i \ssr{}.

Här nedan ges den fullständiga programkoden för att beviset av att den
rekursiva delen av algoritmen är korrekt, givet vissa lemman:
\lstset{numbers=left}
\begin{lstlisting}[escapeinside={@}{@}]
@\label{lst:rad1}@Lemma toom_cook_rec_correct : forall (n : nat) p q,
@\label{lst:rad2}@  unitmx V_I -> toom_cook_rec n p q = p * q.
@\label{lst:rad3}@Proof.
@\label{lst:rad4}@  elim=> [ // | n IHn p q V_inver] /=.
@\label{lst:rad5}@  set b := exponent m p q.
@\label{lst:rad6}@  set u := split m b p.
@\label{lst:rad7}@  set v := split m b q.
@\label{lst:rad8}@  case: ifP => [ // | _ ].
@\label{lst:rad9}@    have ->:
@\label{lst:rad10}@      \col_i toom_cook_rec n ((evaluate u) i 0) ((evaluate v) i 0) = \col_i ((evaluate u) i 0 * (evaluate v) i 0).
@\label{lst:rad11}@        apply/colP => j.
@\label{lst:rad12}@        by rewrite mxE [X in _ = X]mxE (IH2 _ _ V_inver).
@\label{lst:rad13}@      rewrite 2!matrix_evaluation.
@\label{lst:rad14}@      have ->:
@\label{lst:rad15}@        \col_i ((\col_j u.[inter_points j 0]) i 0 * (\col_j v.[inter_points j 0]) i 0) = \col_i (u * v).[(inter_points i 0)].
@\label{lst:rad16}@           apply/colP => k.
@\label{lst:rad17}@           by rewrite 4!mxE -hornerM.
@\label{lst:rad18}@      rewrite toom_cook_interpol //; last by apply: size_split_mul.
@\label{lst:rad19}@      rewrite /recompose hornerM.
@\label{lst:rad20}@      rewrite 2?recompose_split //.
@\label{lst:rad21}@      rewrite /b exponentC.
@\label{lst:rad22}@      by apply: exp_m_degree.
@\label{lst:rad23}@      by apply: exp_m_degree.
@\label{lst:rad24}@Qed.
\end{lstlisting}
\lstset{numbers=none}
På rad~\ref{lst:rad1} och \ref{lst:rad2} och formuleras lemmat och på rad
\ref{lst:rad3} anges att beviset startar genom \C{Proof}. Rad \ref{lst:rad4}
anger med \C{elim=>} att beviset kommer göras med induktion över den variabel
som står först i lemmat, \C{n}. Basfallet är trivialt precis som i
proposition~\ref{prop:1} eftersom \C{toom_cook_rec} är vanlig multiplikation när
\C{n}$\ =0$ och delmålet det utgör avslutas direkt med hjälp av \C{//}. Till
höger om \C{|} ges instruktioner till induktionssteget. Variabeln \C{n'} där
\C{n' + 1}  = \C{n}, för det \C{n} som induktionen görs över instansieras.
Induktionsantagandet \C{IHn}, polynomen \C{p} och \C{q} och antagandet
\C{V_inver} om att interpolationsmatrisen är inverterbar instansieras också.
Till sist skriver \C{/=} om målet genom att utveckla definitionen av
\C{toom_cook_rec}.

Raderna \ref{lst:rad5} till \ref{lst:rad7} sätter kortare beteckningar på några
av uttrycken i målet för att öka bevisets läslighet. Efter det har målet och
kontexten blivit:
\begin{lstlisting}
n' : nat
IHn : forall p q, unitmx V_I -> toom_cook_rec n' p q = p * q
p : {poly R}
q : {poly R}
V_inver : unitmx V_I
b := exponent m p q : nat
u := split m b p : {poly {poly R}}
v := split m b q : {poly {poly R}}
______________________________________(1/1)
(if (size p <= 2) || (size q <= 2)
  then p * q
  else recompose b
    (interpolate
      (\col_i toom_cook_rec n'
        ((evaluate u) i 0)
        ((evaluate v) i 0)))) = p * q
\end{lstlisting}
Nästa steg är sedan att skriva om målet så att vi kan använda
induktionsantagandet \C{IHn}.

Först tar vi hand om \C{if ... then ... else} - uttrycket i vänsterledet.
Det säger att vänsterledet är lika med \C{p * q} om graden
av \C{p} eller \C{q} är mindre än 2 (eftersom multiplikation i algoritmen
då utförs direkt enligt definitionen) och annars
lika med det mer komplicerade uttrycket som står efter \C{else}. Dessa två
uttryck skulle motsvarat basfallet och induktionssteget om induktionen hade
gjorts över graden av p och q, som i proposition~\ref{prop:1}.

Nu görs istället på rad~\ref{lst:rad8} en falluppdelning över
\C{if ... then ... else}-uttrycket, med \C{ifP}, som inte ger oss något nytt
induktionsantagande. I det första fallet när \C{(size p <= 2) || (size q <= 2)}
är höger- och vänsterled i målet trivialt lika och löses liksom det triviala
målet på rad \ref{lst:rad4} direkt med \C{//}. När det är gjort är målet:
\begin{lstlisting}
recompose b
  (interpolate
    (\col_i
      toom_cook_rec n'
        ((evaluate u) i 0)
        ((evaluate v) i 0))) = p * q
\end{lstlisting}
och vi vill använda induktionsantagandet
\begin{lstlisting}
IHn : forall p q, unitmx V_I -> toom_cook_rec n' p q = p * q
\end{lstlisting}
tillsammans med antagandet \C{V_inver} om att \C{V_I} är inverterbar för att
skriva om
\begin{lstlisting}
toom_cook_rec n' ((evaluate u) i 0) ((evaluate v) i 0)
\end{lstlisting}
till \C{((evaluate u) i 0) * ((evaluate v) i 0)} inne i kolonnvektorn
\begin{lstlisting}
\col_i toom_cook_rec n' ((evaluate u) i 0)((evaluate v) i 0)
\end{lstlisting}
Eftersom definitionen \C{\\col_i} är låst för beräkning och uttrycket beror av
indexet \C{i} i kolonnvektorn kan vi inte direkt skriva om uttrycket, se
avsnitt~\ref{sec:ssr}. Så för att kunna göra det öppnar vi ett nytt delmål med
taktiken \C{have} på rad~\ref{lst:rad9} där vi bara arbetar med denna del av
vänsterledet. Där använder vi på rad~\ref{lst:rad11} lemmat \C{colP} som låter
oss visa att två vektorer är lika genom att visa att elementen på motsvarande
platser är lika i båda vektorerna. (Då vektorer är implementerade i \ssr{} som
funktioner från mängden av index i vektorn $\{0, \ldots , k\}$ till mängden av
element i vektorn säger lemmat mer specifikt att två vektorer är lika om de
extensionellt sett är samma funktion, det vill säga om de antar samma värden
för samma argument). På rad~\ref{lst:rad12} kan vi sedan slutligen visa att
uttrycket i vänsterledet i \C{have}-satsen är lika med uttrycket i högerledet i
\C{have}-satsen med hjälp av induktionshypotesen. Då har målet blivit:
\begin{lstlisting}
recompose b
  (interpolate
    (\col_i
      ((evaluate u) i 0 * (evaluate v) i 0))) = p * q
\end{lstlisting}

Det återstår nu att visa tre saker: För det första att evalueringsfunktionen
\C{evaluate} faktiskt ger tillbaka en vektor med ett polynom evaluerat i
interpolationspunkterna, för det andra att interpolationsfunktionen
\C{interpolate} givet dessa vektorer ger oss koefficienterna i produktpolynomet
\C{u * v} och för det tredje att \C{recompose}-funktionen under vissa
förutsättningar är vänsterinvers till \C{split}-funktionen (eftersom \C{u} och
\C{v} ju fås från \C{p} och \C{q} genom \C{split}).

Det första och det andra som ska visas är enkla följder av definitionerna av
matrismultiplikation och -invers och visas inte explicit i det informella
beviset. I det formella beviset visas detta i två lemman, \C{matrix_evaluation}
och \C{toom_cook_interpol}, som används på rad~\ref{lst:rad13} respektive
rad~\ref{lst:rad18} att skriva om målet till
\begin{lstlisting}
recompose b
  (interpolate (\col_i (u * v).[inter_points i 0])) = p * q
\end{lstlisting}
och sedan till
\begin{lstlisting}
recompose b (u * v) = p * q
\end{lstlisting}
Lemmat \C{toom_cook_interpol} har som antagande att graden på det polynom vars
koefficienter ska hittas genom interpolation, $u \cdot v$, är mindre än antalet
interpolationspunkter, så när det åberopas uppkommer ytterligare ett delmål
att visa:
\begin{lstlisting}
______________________________________(2/3)
size (u * v) <= number_points
\end{lstlisting}
Det görs på rad~\ref{lst:rad18} genom lemmat \C{size_split_mul}. Detta är
ytterligare något som är självklart i det informella beviset, eftersom vi har
definierat $u$ och $v$ så att det är uppfyllt. Vi behöver också visa att
interpolationsmatrisen \C{V_I} är inverterbar, men eftersom det är ett av
antagandena i kontexten är det trivialt och visas på rad~\ref{lst:rad18} med
\C{//}.

Sedan utvecklar vi på rad~\ref{lst:rad19} definitionen av \C{recompose} och använder
lemmat \C{hornerM} som säger att $(p \cdot q)(x) = p(x) \cdot q(x)$, för att
skriva om målet till:
\begin{lstlisting}
u.['X^b] * v.['X^b] = p * q
\end{lstlisting}
% Lång krånglig mening
Då kan vi på rad~\ref{lst:rad20} använda lemmat \C{recompose_split}, som ungefär
motsvarar lemma~\ref{lemma:2} i det informella beviset, för att visa att
``sammansättningen'' av de uppdelade polynomen genom att evaluera i $x^b$ är
korrekt och därmed skriva om \C{u.['X^b]} till \C{p} och \C{v.['X^b]} till
\C{q}. Då har vi visat att vänsterledet i det ursprungliga målet är lika med
högerledet \C{p * q}.

Det som återstår är sedan att bevisa motsvarigheten till Påstående 1 i
lemma~\ref{lemma:2}, som gör att villkoren för att \C{recompose_split} ska
kunna användas på \C{p} och \C{q} är uppfyllda.
\begin{lstlisting}
______________________________________(1/2)
size q <= m * b
______________________________________(2/2)
size p <= m * b
\end{lstlisting}
Det görs på rad~\ref{lst:rad22} till \ref{lst:rad23} med lemmat
\C{exp_m_degree} som säger i stort sett detta, efter att först på
rad~\ref{lst:rad21} ha skrivit om det ena av målet med ett lemma för att
funktionen \C{exponent} är kommutativ, vilket avslutar det formella beviset för
att Toom-Cook ger ett korrekt resultat.

\subsubsection{Lemman till huvudbeviset}
Förutom de 50-tal lemman ur \ssr{}-biblioteket som har används i huvudbeviset har
ett 15-tal specifika lemman till huvudsatsen formulerats och bevisats. Här
presenteras några av dem. Det viktigaste lemmat är
\begin{lstlisting}
Lemma recompose_split : forall (f: {poly R}) (b: nat),
  size f <= m * b ->
  (split m b f).['X^b] = f.
\end{lstlisting}
som säger att \C{recompose} (som evaluerar ett polynom i $x^b$) är
vänsterinvers till \C{split} för polynom som har tillräckligt låg grad jämfört
med argumenten \C{m} och \C{b}. Detta motsvarar ungefär lemma~\ref{lemma:2} i
det informella beviset om man hade haft Påstående 1 som ett antagande istället
för att visa det.

Beviset av \C{recompose_split} bygger på fem mindre lemman som motsvarar
delpåståenden i lemma~\ref{lemma:2} och dessutom några lemman som visats av
projektets handledare Anders Mörtberg eftersom de bygger på grundläggande
egenskaper hos \ssr{}-funktionen \C{rdivp} för polynomdivision som gick utanför
projektets ramar.
%Ramar? Eller hur säger man? Å.
\C{recompose_split_lemma1} används för att visa \C{recompose_split_lemma2}, som
sedan används för att visa \C{recompose_split_lemma3}, som används för att visa
huvudlemmat tillsammans med motsvarigheten till Påstående 1, som visas separat
i
\begin{lstlisting}
Lemma exp_m_degree_lemma : forall p,
  m > 0 ->
  size p <= m * succn (size p %/ m).
\end{lstlisting}

Det första lemmat \C{recompose_split_lemma1} är en specik instans av
(\ref{eq:name8}), dvs av att
\begin{align*}
  a(x) = a(x) \modu x^b + \left(a(x)/x^b\right) x^b
\end{align*}.
\begin{lstlisting}
Lemma recompose_split_lemma1 : forall (f: {poly R}) (k b: nat),
  (rmodp (rdivp f 'X^(k*b)) 'X^(b)) * 'X^(k*b) +
  (rdivp f 'X^(k.+1*b)) * 'X^(k.+1*b) =
  (rdivp f 'X^(k*b)) * 'X^(k*b).
\end{lstlisting}
Det andra lemmat motsvarar att (\ref{eq:name9}) = (\ref{eq:name10}), det vill
säga induktionssteget i lemma~\ref{lemma:2}.
\begin{lstlisting}
Lemma recompose_split_lemma2 : forall (f: {poly R}) (k b: nat),
  \sum_(i < k.+1)
    (rmodp (rdivp f 'X^(i*b)) 'X^b) * 'X^(i*b) +
    (rdivp f 'X^(k.+1*b)) * 'X^(k.+1*b) =
  \sum_(i < k)
    (rmodp (rdivp f 'X^(i*b)) 'X^b) * 'X^(i*b) +
    (rdivp f 'X^(k*b)) * 'X^(k*b).
\end{lstlisting}
Det tredje lemmat svarar mot att (\ref{eq:name9}) = $p(x)$.Givet
induktionssteget som görs i \C{recompose_split_lemma2} och basfallet som ges av
\C{recompose_split_lemma1} så fås
\begin{lstlisting}
Lemma recompose_split_lemma3 : forall (f : {poly R}) (k b : nat),
  \sum_(i < k.+1)
    (rmodp (rdivp f 'X^(i*b)) 'X^b)*'X^(i*b) +
    (rdivp f 'X^(k.+1*b))*'X^(k.+1*b)
  = f.
\end{lstlisting}
Detta används slutligen för att visa \C{recompose_split}.
% Precis som beviset av huvudsatsen för att visa likhet mellan vektorer får vi
% här
% använda liknande trick för att visa likhet mellan summor genom att visa att motsvarande
% term i respektive summa är lika.

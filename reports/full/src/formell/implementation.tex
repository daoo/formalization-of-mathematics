Här beskrivs det huvudresultatet i projektet: den formella implementationen och
beviset av Toom-Cook i \coq{} med \ssr{}. Den fungerar också som en
illustration på hur definitioner och bevis ser ut och fungerar i \coq{} med
\ssr{}. De viktigaste funktionerna och lemmana till algoritmen presenteras. Den
fullständiga källkoden finns i \ref{appendix:coqkod}.

\section{Implementation av Toom-Cook i \coq{} med \ssr{}}
\label{sec:formellimplementation}
I det här avsnittet förklaras hur Toom-Cook-algoritmen är implementerad i
\ssr{} och hur den överrenstämmer med den informella definitionen av
algoritmen.

\subsection{Inledande definitioner och funktioner}
\label{section:forminl}
Algoritmen är implementerad med en rad små funktioner, en rekursiv del och
sedan en huvudfunktion. Till en början kommer alltså många variabler,
definitioner och antaganden radas upp, som vi har försökt att ge så
självförklarande namn som möjligt.

\begin{lstlisting}
Section toomCook.
Variable R : idomainType.
Implicit Types p q : {poly R}.
\end{lstlisting}

Först öppnas en ny sektion, inom vilken vi kan låta De här raderna säger att vi
låter \C{R} beteckna ett godtyckligt men fixt integritetsområde och att
variabelnamnen \C{p} och \C{q} alltid kommer beteckna polynom i detta område,
så vi inte behöver ange typen på \C{p} och \C{q} när vi använder de namnen.

\begin{lstlisting}
Variable number_splits : nat.
Definition m : nat := number_splits.
Definition number_points := (2 * m) .-1.
\end{lstlisting}

Sedan parametriseras funktionen på \C{m} genom att ange den som ett godtyckligt
men fixt naturligt tal. \C{m} är den parameter som precis som i den informella
algoritmen anger vilken \toomm{}-instans vi har. Det gör att vi inom sektionen
inte explicit behöver ge \C{m} som argument till varje funktioner vi definerar,
men att vi ändå kan använda det i funktionerna. Vi definerar också
\C{number_points} som det antal interpolationspunkter vi behöver för \toomm{}.

% definieras variabler av typen nat, naturliga
% tal\footnote{I \coq{} och \ssr{}
% är 0 inkluderad bland de naturliga talen}, vars värde beror på antalet splittar
% som görs av polynomet.

\begin{lstlisting}
Variable inter_points : 'cV[{poly R}]_(number_points).
\end{lstlisting}

Vi låter \C{inter_points} beteckna en kolonnvektor vars element tillhör $R[x]$.
Detta ska vara en vektor som innehåller interpolationspunkterna. I den
informella algoritmen låter vi interpolationspunkterna tillhöra $R$. Det gjorde
det lättare att visa att graden på polynomen skulle minska vid rekursiva anrop
av Toom-Cook, vilket krävdes för induktionsantagandet som var över graden på
polynomen, se avsnitt~\ref{in:induktion}. Det kommer dock visa sig längre ner
att det i formella fallet är lämpligare att definiera interpolationspunkterna
som element i $R[x]$. För att algoritmen ska vara tillräckligt snabb ska de
dock vara konstanta polynom (alltså egentligen element i $R$) eller av låg
grad.

% att vilket faktiskt skiljer sig från pappersalgoritmen. Det var avgörandet för
% pappersalogritmen att punkterna tillhörde $R$ för att graden garanterat skulle
% avta vilket ledde till att induktionsantagandet över graden kunde appliceras,
% se avsnitt~\ref{in:induktion}, men i beviset i\ssr görs induktion över en annan
% variabel så det problemet uppstår aldrig. Algoritmen blir dock snabbare om
% interpolationspunkterna tillhör $R$ eller är av låg grad i $R[x]$, se
% avsnitt~\ref{def}.

\begin{lstlisting}
Hypothesis m_neq_0 : 0 < m.
\end{lstlisting}

I den informella algoritmen har vi som antagande att \C{m} är större än två och
här kan vi återigen uttnyttja att induktionen görs över en annan variabel än
graden, då kommer vi runt problemet i lemma (\ref{lemma:1}), och kan vi få ner
kravet till att \C{m} måste vara större än noll.

\begin{lstlisting}
Definition V_e : 'M[{poly R}]_(number_points, m) :=
  \matrix_(i < number_points, j < m) ((inter_points i 0))^+j.
\end{lstlisting}
Här definieras evalueringsmatrisen \C{V_e} som en \C{number_points} x \C{m}-matris
med element som tillhör $R[x]$.
\C{\\matrix_(i < number_points, j < m)((inter_points i 0))^+j} säger att matrisen
på plats \C{(i,j)}, med \C{(i < number_points)} och \C{(j < m)} ska ha \C{i}:te
elementet ur kolonnvektorn \C{inter_points} upphöjt till \C{j}.

\begin{lstlisting}
Definition V_I : 'M[{poly R}]_(number_points) :=
  \matrix_(i < number_points, j < number_points) ((inter_points i 0))^+j.
\end{lstlisting}

Här definieras interpolationsmatrisen på motsvarade sätt som
evalueringsmatrisen, skillnaden är att det blir en matris av dimension
\C{number_points} x \C{number_points} istället.

\begin{lstlisting}
Definition exponent (m: nat) p q : nat :=
  (maxn (divn (size p) m) (divn (size q) m)).+1.
\end{lstlisting}
Här definieras funktionen \C{exponent}. Den motsvarar det $b$ som definieras i
ekvation~(\ref{eq:b}) i steg~\ref{in:uppdelning} om den anropas med m:et i
\toomm{}. \C{size p} tar graden av polynomet \C{p} + 1, \C{divn} är exakt
division för naturliga tal och \C{maxn} ger det största av två naturliga tal.
Funktionerna \C{size}, \C{divn} och \C{maxn} är implementerade i \ssr
bibliotek. Beteckningen \C{k.+1} står för efterföljaren till \C{k}.

\begin{lstlisting}
Definition split (n b: nat) p : {poly {poly R}} :=
  \poly_(i < n) rmodp (rdivp p 'X^(i * b)) 'X^b.
\end{lstlisting}
\C{split} tar två naturliga tal och ett polynom som inparametrar och returnerar
ett polynom i $(R[x])[y]$. \C{rmodp} och \C{rdivp} ger oss kvoten och resten
vid polynomdivision se ekvationerna (\ref{eq:rmod}) och (\ref{eq:rdiv}).
Funktionerna \C{\\poly_(i<n)}, \C{rmodp} och \C{rdivp} bildar tillsammans
motsvarande polynom
$u(y)=(p/x^0\;mod\;x^b)y^0+...+(p/x^{(n-1)b}\;mod\;x^b)y^{n-1}$ där \C{n} och
\C{b} är inparametrarna.

\begin{lstlisting}
Definition evaluate (u: {poly {poly R}}) : 'cV[{poly R}]_(number_points) :=
  V_e *m (poly_rV u)^T.
Definition interpolate (u: 'cV[{poly R}]_(number_points)) : {poly {poly R}} :=
  rVpoly (invmx V_I *m u)^T.
\end{lstlisting}

Här definieras själva matrismultiplikationerna, se ekvation (\ref{align:eval})
och (\ref{eq:NAME5}). \C{evaluate} ger tillbaka en vektor med ett polynoms
värden i interpolationspunkterna. Funktionen \C{poly_rV} ger tillbaka en
radvektor med ett polynoms koefficienter (motsvarande funktion finns inte
implementerad i \ssr för kolonnvektorer, och för att kunna använda resultat
från biblioteket i bevisen använder vi den i stället för att definiera en egen
kolonnversion). \C{^T} transponer radvektorn för att kunna multiplicera den med
evalueringsmatrisen.

Matrismultiplikation är bara definierat i \ssr mellan matriser och vektorer av
samma typ. Eftersom polynomet \C{u} som ska evalueras har koefficienter i
$R[x]$, så att vektorn av dess koefficienter ligger i $R[x]$, måste matrisens
element också ligga i $R[x]$. Det är därför som vi låter
interpolationspunkterna ligga i $R[x]$ här i ställer för i $R$. Det hade gått
att lösa det på andra sätt, men det hade gjort bevisen för att \C{evaluate} och
särskiljt \C{interpolate} mycket mer komplicerade.

\begin{lstlisting}
Definition recompose (b: nat) (w: {poly {poly R}}) : {poly R} :=
  w.['X^b].
\end{lstlisting}
<<<<<<< HEAD
Här sker evalueringen av \C{w} i $x^b$ där \C{b} är en inparameter.

\subsection{Den rekursiva funktionen och huvudfunktionen}
% Fjärde steget i Toom-Cook-algoritmen. se avsnitt~\ref{in:rekursiv}, är en rekusiv del och
% rekusiv del och därmed behövs en rekusiv funktion,
% nämligen Fixpoint, för att
% kunna stega igenom hela algoritmen. I pappersalgoritmen slutar rekusionen när
% graden har blivit tillräckligt låg och då utförs direktmultiplikation.

Nu har hjälpfunktionerna till algoritmen definierats. Nu definieras själva
algoritmen. Det görs med hjälp av två funktioner. En rekursiv del (som i \coq{}
definieras med kommandot \C{Fixpoint}) som egentligen gör allt jobbet, och en
yttre del som bara anropar den rekursiva delen med ett visst argument.
Anledningen till detta är att \coq{} bara tillåter rekursiva funktioner som
säkert terminerar. Antingen behöver \coq{} lätt förstå att något av argumenten
i funktionen avtar mot ett basfall med varje rekursivt anrop, eller så får
användaren själv bevisa att det hon eller han vill ska vara
rekursionsparametern, som i det här fallet är graden på polynomen, minskar.

Ett sätt att komma runt detta är att definiera funktionen med ett extra
argument, i det här fallet \C{n}, ett naturligt tal vars enda uppgift är att
minskas med 1 för varje rekursivt anrop av funktionen. Dessutom definierar man
två basfall för rekursionen. Ett för när det extra argumentetet \C{n} når 0,
och ett för den verkliga rekursionsparametern. Då godkänner \coq{} funktionen,
och användaren slipper göra ett krångligt bevis för att rekursionsparametern
avtar.

Sedan definierar man en yttre funktion, som anropar den rekursiva funktionen och
ger det extra argumentet värdet av den riktiga rekursionsparametern.

I \C{toom_cook_rec} är basfallen \C{n} når 0 och att graden av något av polynomen
blir mindre än 2. Då utförs vanlig polynommultiplikation. Falluppdelningen mellan
basfall och rekursionsfall för extraargumentet \C{n} görs med en \C{match}-sats.
Sedan görs falluppdelning mellan basfall och rekursionsfall för den riktiga
rekursionsparametern med en \C{if ... then ... else}-sats. Nedan beskrivs
stegen i funktionen ett och ett.
=======

Här sker evalueringen av \C{w} i $x^b$ där \C{b} är en inparametern.

\subsection{Rekusiv del}
% Första meningen kan behöva ändras
Fjärde steget i Toom-Cook-algoritmen. se avsnitt~\ref{in:rekursiv},  är en rekusiv del och
rekusiv del och därmed behövs en rekusiv funktion, nämligen Fixpoint, för att
kunna stega igenom hela algoritmen. I pappersalgoritmen slutar rekusionen när
graden har blivit tillräckligt låg och då utförs direktmultiplikation. \coq
behöver få bevisat att rekusionen terminar så istället för att visa att graden
sjunker för varje steg så skickas ett naturligt tal \C{n}, som inparameter, som
minskar vid varje rekusion och slutar när \C{n} är lika med noll eller när
graden har blivit mindre än 2 på någotdera av polynomen, och då utförs direkt
multiplikation. Så för att visa att Toom-Cook-algoritmen fungerar måste
algoritmen fungera för alla \C{n} eftersom fixpoint-funktion bara kommer motsvara
Toom-Cook om \C{n} är tillräckligt stort, ty tillräckligt många rekursiva anrop
måste göras.

% Lite skumt skrivet
Funktionen match kollar om \C{n} är lika med 0 i så fall retunera \C{p} gånger
\C{q} eller om \C{n} är en efterföljare till något tal.

>>>>>>> Changes to the CoqIde part

\begin{lstlisting}
Fixpoint toom_cook_rec (n: nat) p q : {poly R} :=
  match n with
  | 0%N => p * q
  | n'.+1 => if (size p <= 2) || (size q <= 2) then p * q else
        let b   := exponent m p q in
        let u   := split m b p in
        let v   := split m b q in
        let u_a := evaluate u in
        let v_a := evaluate v in
        let w_a := \col_i toom_cook_rec n' (u_a i 0) (v_a i 0) in
        let w   := interpolate w_a
         in recompose b w
  end.
\end{lstlisting}

\begin{lstlisting}
| 0%N => p * q
\end{lstlisting}
Om en \C{n} är lika med 0 så sker direkt multiplikation mellan polynomen.

\begin{lstlisting}
| n'.+1 => if (size p <= 2) || (size q <= 2) then p * q else
\end{lstlisting}
Om \C{n} är \C{n'.+1}, det vill säga en efterföljare till något naturligt tal
\C{n'}, med andra ord större än 0, så kontrolleras graden på polynomen. Det
motsvarar steg~\ref{in:gradkontroll} i den informella algoritmen. Om graden på
något av polynomen är mindre än 2 multipliceras de direkt. Annars går vi vidare
i algoritmen.
\begin{lstlisting}
let b := exponent m p q in
\end{lstlisting}
Här låter vi \C{b} få värdet från funktionen \C{exponent} vilket som nämnts
ovan är samma värde som variabeln $b$ får i den informella algoritmen i
ekvation~(\ref{eq:b}), eftersom \C{m} är $m$:et i \toomm{} och \C{p} och \C{q} är
polynomen som ska multipliceras.
\begin{lstlisting}
let u := split m b p in
let v := split m b q in
\end{lstlisting}
Härnäst definierar vi som i den informella algoritmen \C{u} och \C{v}, som fås
från \C{p} och \C{q} genom uppdelningen i ekvationerna (\ref{eq:u}) och
(\ref{eq:v}) i steg~\ref{in:uppdelning}.

\begin{lstlisting}
let u_a := evaluate u in
let v_a := evaluate v in
\end{lstlisting}

Här sker motsvarigheten till steg~\ref{in:evaluering}, evalueringen av \C{u}
och \C{v} i interpolationspunkterna. \C{u_a} och \C{v_a} kommer vara
kolonnvektorer.

\begin{lstlisting}
let w_a := \col_i toom_cook_rec n' (u_a i 0) (v_a i 0) in
\end{lstlisting}

Det här motsvarar steg~\ref{in:rekursiv} i den informella algoritmen, den
rekursiva multiplikationen. \C{w_a} är en vektor som på plats \C{i} har
resultatet av att anropa \C{toom_cook_rec} igen, nu med \C{n'} som inparameter,
vilken har värdet n-1, och \C{i}:te elementet i \C{u_a} och \C{v_a}. (\C{(u_a i
0)} och \C{(v_a i 0)} är \C{u} och \C{v} evaluerade den i \C{i}:te
interpolationspunkten.)
\begin{lstlisting}
let w := interpolate w_a
in recompose b w
\end{lstlisting}
När rekusionssteget är klart så sker interpolerationssteget som motsvarar
steg~\ref{in:interpol}, där inversen av interpolationsmatrisen multipliceras
med kolonnvektorn \C{w_a} och retunerar polynomet \C{w} som är produkten av
polynomen u och v. Till sist sker sammansättningen som motsvarar steg~
\ref{in:sammansattning}, där polynomet \C{w} evalueras i $x^b$, och därmed har
vi fått produkten av polynomen \C{p} och \C{q}.

Slutligen definieras den yttre funktionen \C{toom_cook}, där \C{toom_cook_rec} anropas med
maximum av graden av polynomen plus 1.
\begin{lstlisting}
 Definition toom_cook p q : {poly R} :=
  toom_cook_rec (maxn (size p) (size q)) p q.
\end{lstlisting}

\section{\textsc{Haskell}-kod}
\subsection{ToomCook.hs}
\label{sec:haskell}
\begin{verbatim}
{-# LANGUAGE LambdaCase, BangPatterns,
             MagicHash, UnboxedTuples #-}
module ToomCook where

import Data.Ratio
import GHC.Base
import GHC.Integer

matVecMul :: Num a => [[a]] -> [a] -> [a]
matVecMul mat vec = map (sum . zipWith (*) vec) mat

log10 :: Integer -> Integer
log10 n = assert (n >= 0) $ go 0 n
  where
    go !acc  0  = acc
    go !acc !n' = go (acc + 1) (n' `quotInteger` 10)

data ToomCook = ToomCook
  { toomK :: Int
  , toomMat :: [[Integer]]
  , toomInvMat :: [[Rational]]
  }

{-# INLINE baseExponent #-}
baseExponent :: Int -> Integer -> Integer -> Integer
baseExponent k n m = assert (k > 0) $
  1 + max
    (log10 n `quotInteger` fromIntegral k)
    (log10 m `quotInteger` fromIntegral k)

{-# INLINE split #-}
-- |Split an integer into a big-endian list of integers using a given base
split :: Int -> Integer -> Integer -> [Integer]
split k b = assert (b > 0) $ go k []
  where
    go  0  acc _ = acc
    go !k' acc n = case n `quotRemInteger` b of
      (# n', x' #) -> go (k'-1) (x':acc) n'

-- |Merge a splitted integer using some base
-- This is the inverse of split.
merge :: Integer -> [Integer] -> Integer
merge b = recompose b . reverse

{-# INLINE evaluate #-}
-- |Evaluate the splits using the evaluation matrix
-- Note that since the splitlist is big-endian, either we have
-- to reverse each row of the evaluation matrix, or reverse the
-- splitlist.
evaluate :: [[Integer]] -> [Integer] -> [Integer]
evaluate = matVecMul

{-# INLINE interpolate #-}
interpolate :: [[Rational]] -> [Integer] -> [Integer]
interpolate mat vec = map numerator $ matVecMul mat $ map fromIntegral vec

{-# INLINE recompose #-}
recompose :: Integer -> [Integer] -> Integer
recompose b = go 1 0
  where
    go  _  !acc []     = acc
    go !b' !acc (x:xs) = go (b * b') (acc + b' * x) xs

{-# INLINE toomCook #-}
toomCook :: ToomCook -> Integer -> Integer -> Integer
toomCook !t !n !m | n < 0 && m < 0 = toomCook t (abs n) (abs m)
                  | n < 0          = negate $ toomCook t (abs n) m
                  | m < 0          = negate $ toomCook t n (abs m)

                  | n <= 100 || m <= 100 = n * m

                  | otherwise =
  let b   = 10^(baseExponent (toomK t) n m)
      n'  = split (toomK t) b n
      m'  = split (toomK t) b m
      n'' = evaluate (toomMat t) n'
      m'' = evaluate (toomMat t) m'
      r   = zipWith (toomCook t) n'' m''
      r'  = interpolate (toomInvMat t) r
   in recompose b r'
\end{verbatim}

\subsection{Settings.hs}
\begin{verbatim}
module Settings where

import Data.Ratio
import ToomCook

-- NOTE: The rows in the evaluation matrices are inverted

toom1 :: ToomCook
toom1 = ToomCook 1 [[1]] [[1]]

karatsuba :: ToomCook
karatsuba = ToomCook 2
  [ reverse [ 1 , 0 ]
  , reverse [ 1 , 1 ]
  , reverse [ 0 , 1 ]
  ]
  [ [1  , 0 , 0  ]
  , [-1 , 1 , -1 ]
  , [0  , 0 , 1  ]
  ]

toom3 :: ToomCook
toom3 = ToomCook 3
  [ [ 0 , 0  , 1 ]
  , [ 1 , 1  , 1 ]
  , [ 1 , -1 , 1 ]
  , [ 4 , -2 , 1 ]
  , [ 1 , 0  , 0 ]
  ]
  [ [ 1    , 0   , 0   , 0    , 0  ]
  , [ 1%2  , 1%3 , -1  , 1%6  , -2 ]
  , [ -1   , 1%2 , 1%2 , 0    , -1 ]
  , [ -1%2 , 1%6 , 1%2 , -1%6 , 2  ]
  , [ 0    , 0   , 0   , 0    , 1  ]
  ]

toom4 :: ToomCook
toom4 = ToomCook 4
  [ reverse [ 1 , 0  , 0 , 0  ]
  , reverse [ 1 , 1  , 1 , 1  ]
  , reverse [ 1 , -1 , 1 , -1 ]
  , reverse [ 1 , -2 , 4 , -8 ]
  , reverse [ 1 , 2  , 4 , 8  ]
  , reverse [ 1 , 3  , 9 , 27 ]
  , reverse [ 0 , 0  , 0 , 1  ]
  ]
  [ [ 1     , 0     , 0     , 0      , 0     , 0     , 0   ]
  , [ -1%3  , 1     , -1%2  , 1%20   , -1%4  , 1%30  , -12 ]
  , [ -5%4  , 2%3   , 2%3   , -1%24  , -1%24 , 0     , 4   ]
  , [ 5%12  , -7%12 , -1%24 , -1%24  , 7%24  , -1%24 , 15  ]
  , [ 1%4   , -1%6  , -1%6  , 1%24   , 1%24  , 0     , -5  ]
  , [ -1%12 , 1%12  , 1%24  , -1%120 , -1%24 , 1%120 , -3  ]
  , [ 0     , 0     , 0     , 0      , 0     , 0     , 1   ]
  ]
\end{verbatim}

\subsection{Properties.hs}
\begin{verbatim}
module Main where

import Settings
import Test.QuickCheck
import ToomCook

genK :: Gen Int
genK = choose (2, 10)

genNum :: Gen Integer
genNum = choose (100000000, 999999999)

propToomCookCorrect :: ToomCook -> Property
propToomCookCorrect t = forAll genNum $ \n -> forAll genNum $ \m ->
  toomCook t n m == n * m

propSplitCorrect :: Property
propSplitCorrect = forAll genK $ \k -> forAll genNum $ \n ->
  let b = 10^baseExponent k n n in n == merge b (split k b n)

main :: IO ()
main = do
  quickCheck (propToomCookCorrect karatsuba)
  quickCheck (propToomCookCorrect toom3)
  quickCheck (propToomCookCorrect toom4)
  quickCheck propSplitCorrect
\end{verbatim}

\section{Toom-$n$ in Haskell}
\subsection{ToomCookNat.hs}
\begin{verbatim}
{-# LANGUAGE LambdaCase #-}
module ToomCookNat where

import Data.List
import Data.Ratio

data ToomCook = ToomCook
  { toomK :: Int
  , toomMat :: [[Integer]]
  , toomInvMat :: [[Rational]]
  }

matVecMul :: Num a => [[a]] -> [a] -> [a]
matVecMul mat vec = map (sum . zipWith (*) vec) mat

unsafeToInteger :: Rational -> Integer
unsafeToInteger r | denominator r == 1 = numerator r
                  | otherwise          = error $ show r

degree :: Integer -> Integer
degree = go 0
  where
    go acc 0 = acc
    go acc n = go (acc + 1) (n `div` 10)

baseExponent :: Int -> Integer -> Integer -> Integer
baseExponent k n m = 1 + max
  (degree n `div` fromIntegral k)
  (degree m `div` fromIntegral k)

split :: Int -> Integer -> Integer -> [Integer]
split k b = go k []
  where
    go 0  acc _ = acc
    go k' acc n = let (n', x') = n `divMod` b
                   in go (k' - 1) (x' : acc) n'

merge :: Integer -> [Integer] -> Integer
merge b = recompose b . reverse

evaluate :: [[Integer]] -> [Integer] -> [Integer]
evaluate mat vec = matVecMul mat (reverse vec)

interpolate :: [[Rational]] -> [Integer] -> [Integer]
interpolate mat = map unsafeToInteger . matVecMul mat . map toRational

recompose :: Integer -> [Integer] -> Integer
recompose b = snd . foldl' (\(b', sum) x -> (b * b', sum + b' * x)) (1, 0)

toomCook :: ToomCook -> Integer -> Integer -> Integer
toomCook t n m | n < 0 && m < 0 = toomCook t (abs n) (abs m)
               | n < 0          = negate $ toomCook t (abs n) m
               | m < 0          = negate $ toomCook t n (abs m)

               | n <= 100 || m <= 100 = n * m

               | otherwise =
  let b   = 10^(baseExponent (toomK t) n m)
      n'  = split (toomK t) b n
      m'  = split (toomK t) b m
      n'' = evaluate (toomMat t) n'
      m'' = evaluate (toomMat t) m'
      r   = zipWith (toomCook t) n'' m''
      r'  = interpolate (toomInvMat t) r
   in recompose b r'
\end{verbatim}

\subsection{Properties.hs}
\begin{verbatim}
module Properties where

import Test.QuickCheck
import ToomCookNat

genK :: Gen Int
genK = choose (2, 10)

genNum :: Gen Integer
genNum = choose (100000000, 999999999)

propToomCookCorrect :: ToomCook -> Property
propToomCookCorrect t = forAll genNum $ \n -> forAll genNum $ \m ->
  toomCook t n m == n * m

propSplitCorrect :: Property
propSplitCorrect = forAll genK $ \k -> forAll genNum $ \n ->
  let b = 10^baseExponent k n n in n == merge b (split k b n)
\end{verbatim}

\subsection{Examples.hs}
\begin{verbatim}
module Examples where

import Data.Ratio
import ToomCookNat

wikiSettings :: ToomCook
wikiSettings = ToomCook 3
  [ [ 1 , 0  , 0 ]
  , [ 1 , 1  , 1 ]
  , [ 1 , -1 , 1 ]
  , [ 1 , -2 , 4 ]
  , [ 0 , 0  , 1 ]
  ]
  [ [ 1    , 0   , 0   , 0    , 0  ]
  , [ 1%2  , 1%3 , -1  , 1%6  , -2 ]
  , [ -1   , 1%2 , 1%2 , 0    , -1 ]
  , [ -1%2 , 1%6 , 1%2 , -1%6 , 2  ]
  , [ 0    , 0   , 0   , 0    , 1  ]
  ]

test1Settings :: ToomCook
test1Settings = ToomCook 4
  [ [ 1 , 0  , 0 , 0  ]
  , [ 1 , 1  , 1 , 1  ]
  , [ 1 , -1 , 1 , -1 ]
  , [ 1 , -2 , 4 , -8 ]
  , [ 1 , 2  , 4 , 8  ]
  , [ 1 , 3  , 9 , 27 ]
  , [ 0 , 0  , 0 , 1  ]
  ]
  [ [ 1     , 0     , 0     , 0      , 0     , 0     , 0   ]
  , [ -1%3  , 1     , -1%2  , 1%20   , -1%4  , 1%30  , -12 ]
  , [ -5%4  , 2%3   , 2%3   , -1%24  , -1%24 , 0     , 4   ]
  , [ 5%12  , -7%12 , -1%24 , -1%24  , 7%24  , -1%24 , 15  ]
  , [ 1%4   , -1%6  , -1%6  , 1%24   , 1%24  , 0     , -5  ]
  , [ -1%12 , 1%12  , 1%24  , -1%120 , -1%24 , 1%120 , -3  ]
  , [ 0     , 0     , 0     , 0      , 0     , 0     , 1   ]
  ]

test2Settings :: ToomCook
test2Settings = ToomCook 2
  [ [ 1 , 0 ]
  , [ 1 , 1 ]
  , [ 0 , 1 ]
  ]
  [ [1  , 0 , 0  ]
  , [-1 , 1 , -1 ]
  , [0  , 0 , 1  ]
  ]
\end{verbatim}
